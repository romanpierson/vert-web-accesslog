plugins {
	id 'java'
	id 'maven-publish'
	id 'jacoco'
    id "com.jfrog.bintray" version "1.8.5"
}

repositories {
    mavenCentral()
    jcenter()
}

ext{
	vertxVersion = '4.0.0'
	jupiterVersion = '5.7.0'
	jupiterLauncherVersion ='1.7.0'
	slfApiVersion = '1.7.30'
	jacksonDatabindVersion = '2.12.0'
	logbackVersion = '1.2.3'
}

dependencies {

	implementation "io.vertx:vertx-web:${vertxVersion}"
	implementation "org.slf4j:slf4j-api:${slfApiVersion}"
	
	//testRuntimeOnly "com.mdac:v8s:0.0.3"
	testRuntimeOnly "io.vertx:vertx-config-yaml:${vertxVersion}"
	
	testRuntimeOnly "com.fasterxml.jackson.core:jackson-databind:${jacksonDatabindVersion}"
	
	testImplementation "org.junit.jupiter:junit-jupiter-api:${jupiterVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${jupiterVersion}"
    testImplementation "org.junit.platform:junit-platform-launcher:${jupiterLauncherVersion}"
	
	// Logback (via SLF4J)
	testRuntimeOnly "ch.qos.logback:logback-classic:${logbackVersion}"
	
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

sourceCompatibility='1.8'
targetCompatibility='1.8'

jar.archiveFileName = "vertx-web-accesslog-1.4.0.jar"
bintrayUpload.packageName = "vertx-web-accesslog-1.4.0.jar"

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            
            groupId 'com.mdac'
            artifactId 'vertx-web-accesslog'
            version '1.4.0'
            
            from components.java

            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}


bintray {
	user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
	key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    
    publications = ['mavenJava']
    dryRun = false
    publish = true
    pkg {
        repo = 'maven'
        name = 'com.mdac.vertx-web-accesslog'
        licenses = ['Apache-2.0']
        labels = ['vertx', 'log', 'accesslog']
        publicDownloadNumbers = true
        //Optional version descriptor
        override = false
        vcsUrl = 'https://github.com/romanpierson/vertx-web-accesslog.git'
        version {
        	name = '1.4.0'
        }
    }
}

jacocoTestReport {
	reports {
		xml.enabled = true
		html.enabled = true
	}
}

wrapper() {
  gradleVersion = '6.7.1'
}

